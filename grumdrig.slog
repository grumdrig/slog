/// Target: Bompton Island v1.1
/// Title: Let's Go Hunting
/// Author: Grumdrig
/// Description: Greedily slay beasts to level up at all costs.

startgame(2)

macro initialize(stat, count) {
  var count = count
  while count {
    train(stat)
    count -= 1
  }
}

initialize(StatStrength, 4)
initialize(StatAgility, 1)
initialize(StatEndurance, 1)
initialize(StatIntellect, 2)
initialize(StatWisdom, 1)
initialize(StatCharisma, 1)


macro goto(dest) {
  while travel(dest) > 0 { }
}

var raids = 0

func goodHunting(forLevel) {
  const numGrounds = 8
  var huntingGrounds[numGrounds] = [12, 12, 17, 10, 16, 15, 33, 6]
  if forLevel < numGrounds {
    return huntingGrounds[forLevel]
  } else {
    return huntingGrounds[numGrounds - 1]
  }
}

// while !forage(EquipmentTotem) { }
seekquest()
learn(1, HEAL_YEAH)
// learn(3, BUFF)
// while .InventoryReagents < 4 { forage(InventoryReagents) }
// cast(BUFF)
while .Level < 88 {
  goto(goodHunting(.Level))
  raids += 1
  while .Health >= (2 * .MaxHealth / 3) { // and .Encumbrance < .Capacity {
    while !.MobHealth or .MobLevel > .Level {
      hunt()
    }
    while .MobHealth and .Health >= (.MaxHealth / 3) {
      melee()
    }
    if .Encumbrance < .Capacity and !.MobHealth { loot() }
    while .Health < .MaxHealth {
      while !.InventoryReagents {
        if forage(InventoryReagents) < 0 { break }
      }
      if cast(HEAL_YEAH) <= 0 { break }
    }
    //drop(InventoryTrophies, .InventoryTrophies)
  }
  goto(18)
  levelup()
  while .Health < .MaxHealth {
    rest()
  }
  sell(InventoryTrophies, .InventoryTrophies)
}

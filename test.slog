/////////////////////////////
// Test suite for compiler //
/////////////////////////////

macro checksp() { assert *(-2) == 4096 }

checksp()

var x
var y
var z

// Character literals

assert 'a' == 97
assert 'ab' == 97 * 256 + 98
assert "hello"[1] == 'e'

/////////////////
// Expressions //
/////////////////

// Using variables avoids precomputation
var v0 = 0
var v1 = 1
var v2 = 2
var v3 = 3
var v4 = 4
var v5 = 5
var v6 = 6
var v7 = 7
var v8 = 8
var v9 = 9
var v10 = 10
var v11 = 11
var v12 = 12

///////////////////
// Binary operators
///////////////////

// Arithmetic operators

assert 3 ** 2 == 9
assert 9 %% 2 == 3
assert 2 * 3 == 6
assert 6 / 2 == 3
assert 5 % 3 == 2
assert 5 + 3 == 8
assert 5 - 3 == 2
assert 5 << 2 == 20
assert 20 >> 2 == 5
assert 3 <? 6 == 3
assert 6 <? 3 == 3
assert 3 <? 3 == 3
assert 3 >? 6 == 6
assert 6 >? 3 == 6
assert 3 >? 3 == 3

assert v3 ** v2 == v9
assert v9 %% v2 == v3
assert v2 * v3 == v6
assert v6 / v2 == v3
assert v5 % v3 == v2
assert v5 + v3 == v8
assert v5 - v3 == v2
assert v2 << v2 == v8
assert v8 >> v2 == v2
assert v3 <? v6 == v3
assert v6 <? v3 == v3
assert v3 <? v3 == v3
assert v3 >? v6 == v6
assert v6 >? v3 == v6
assert v3 >? v3 == v3

// Associativity check

assert 5 - 3 - 1 == 1
assert v5 - v3 - v1 == v1
assert 3 || 0 && 5 == 5
assert 3 || 0 && 5 == 5
assert v3 || v0 && v5 == v5

x = 1
y = 2
z = 3
x = y = z
assert x == z

// Comparison operators

assert 4 < 5
assert 4 <= 5
assert 5 <= 5
assert 6 > 5
assert 6 >= 5
assert 5 >= 5
assert 5 == 5
assert 4 != 5

assert v4 < v5
assert v4 <= v5
assert v5 <= v5
assert v6 > v5
assert v6 >= v5
assert v5 >= v5
assert v5 == v5
assert v4 != v5

// Bitwise operators

assert 6 & 3 == 2
assert 6 ^ 3 == 5
assert 6 | 3 == 7

assert v6 & v3 == v2
assert v6 ^ v3 == v5
assert v6 | v3 == v7

checksp()

// Logical operators

assert (6 && 3) == 3
assert !(0 && 3)
assert !(6 ^^ 3)
assert !(0 ^^ 0)
assert (0 ^^ 3) == 3
assert (2 ^^ 0) == 2
assert (6 || 3) == 6
assert (0 || 3) == 3
assert !(0 || 0)

checksp()

assert (v6 && v3) == v3
assert !(v0 && v3)
assert !(v6 ^^ v3)
assert !(v0 ^^ v0)
assert (v0 ^^ v3) == v3
assert (v2 ^^ v0) == v2
assert (v6 || v3) == v6
assert (v0 || v3) == v3
assert !(v0 || v0)

checksp()

// Shortcutting

x = 0
func incX(r) {
	x += 1
	return r
}
incX(0)
assert x == 1
assert incX(1) || incX(1)
assert x == 2
assert incX(1) && incX(1)
assert x == 4
assert !(incX(0) && incX(1))
assert x == 5

checksp()

// Assignment operators

assert (x = 3) == 3
assert (x += 4) == 7
assert (x -= 4) == 3
assert (x *= 4) == 12
assert (x /= 3) == 4
assert (x %= 3) == 1
assert (x <<= 3) == 8
assert (x >>= 1) == 4
assert (x |= 2) == 6
assert (x &= 3) == 2
assert (x ^= 7) == 5

assert (x = v3) == v3
assert (x += v4) == v7
assert (x -= v4) == v3
assert (x *= v4) == v12
assert (x /= v3) == v4
assert (x %= v3) == v1
assert (x <<= v3) == v8
assert (x >>= v1) == v4
assert (x |= v2) == v6
assert (x &= v3) == v2
assert (x ^= v7) == v5

checksp()

/////////////////////
// Prefix operators
/////////////////////

assert +6 == 6
assert +6 == + 6
assert +6 == +(6)
assert -6 == -(6)
assert ~7 == -8
assert ~-8 == 7
assert ~0 == -1
assert ~(~11) == 11
assert !6 == 0
assert !0 == 1
assert ! !0 == 0
assert ! ! -1 == 1

assert +v6 == v6
assert +v6 == + v6
assert +v6 == +(v6)
assert -v6 == -(v6)
assert ~v7 == -v8
assert ~-v8 == v7
assert ~v0 == -v1
assert ~(~v11) == v11
assert !v6 == v0
assert !v0 == v1
assert ! !v0 == v0
assert ! ! -v1 == v1

checksp()

////////////////////
// Functions
///////////////////

func factorial(n) (n <= 1 && 1) || n * factorial(n - 1)
assert factorial(3) == 6

macro plusFive(v) v + 5
assert plusFive(1) == 6
assert plusFive(v1) == v6

checksp()

func either(which) {
  if which {
    return 1
  } else {
    return 2
  }
}

assert either(100) == 1
assert either(0) == 2

func unless(cond, what) {
  if cond {
    return 99
  }
  return what
}

assert unless(1,10) == 99
assert unless(0,20) == 20

checksp()

var glow = 4
macro topMan(n) {
  var glow = 9
  glow += n
}
topMan(2)
assert glow == 4


// Macros

var five = 5
macro more(t) {
  var u = t + 10
  assert u == t + 10
  //assert u[-1] == t
}
more(5)

checksp()


///////////////
// Variables
///////////////

var var1
var var2
var2 = var1 + 2
assert var2 == 2
var var3 = [10, 20, 30]
assert var3[1] == 20

func f1(var4) {
  var var5
  var5 = var2 + 100
  var1 = var4 + var5 + 1000
  return var3
}

assert f1(50) == 10  // yeah that's perverse
assert var1 == 50 + 2 + 100 + 1000

checksp()

// Scoping in blocks

var what = 1
var was = 4
while what {
  var what = 2
  what = 3
  was = 5
  break
}
assert what == 1
assert was == 5

if what {
  var what
  what = 0
  was = 6
}
assert what == 1
assert was == 6

checksp()

func fresh(what) {
  var was = 7
  what = 8
  was = 9
  return what
}
assert what == 1
assert was == 6
what = fresh(what)
assert what == 8


/////////////////
// Pointers
/////////////////

var p1 = [11,22,33]
var p2 = 44
var p3[4]
var p4[4]
var p5[0]

assert * &p1 == 11
assert *(&p1 + 1) == 22
assert &p2 - &p1 == 3
assert &p4 - &p3 == 4
assert &p5 - &p3 == 8

func math(a, b) {
  var c
  var d
  c = a
  d = b
  assert c == a
  assert c[1] == b
  assert a[-1] == b
 }

 math(5,6)



///////////////
// Perverse
///////////////

var perv1 = [10,20,30]
var perv2 = 40

assert perv1 == 10
assert perv1[3] == 40

var pa = [1,2,3]
var pc[0]
var pd = []
var pe[0] = []
var pb = 4

assert &pb - &pa == 3

checksp()

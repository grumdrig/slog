/// Title: Plotline Chester
/// Author: Grumdrig
/// Description: Follow the main plotline as expediently as possible in an attempt to win the game.

target ChinbreakIsland_v1

export "checkbox : Complete the side quest in Act V to gain an elixir"
var Attempt_elixir_side_quest = 0

export "range 1 to 99:Percentage of max health at which to continue to battle"
var Safety_margin = 50

macro goto(dest) {
	while dest != .Location and travel(dest) > 0 { }
}

macro minStat() .Strength <? .Agility <? .Endurance <? .Wisdom <? .Intellect <? .Charisma

func upgradeEquipment() {
	const least = (.Weapon / 5) <? .Armor <? .Shield <? .Headgear <? .Footwear <? .Mount
	const worst =
		(.Weapon / 5) == least and Weapon or
		.Armor == least and Armor or
		.Shield == least and Shield or
		.Headgear == least and Headgear or
		.Footwear == least and Footwear or
		Mount
	return worst == Weapon and buy(Weapon, .worst + 5) or buy(worst, .worst + 1)
}

func seekVictim() {
	while .MobSpecies != .QuestMob or !.MobHealth or (.MobLevel > .Act and .Act <= 1) {
		seek(MobSpecies)
	}
}

while 1 {
	// Train
	while .TrainingPoints > 0 {
		const ms = minStat()
		if ms == .Strength       { train(Strength) }
		else if ms == .Agility   { train(Agility) }
		else if ms == .Endurance { train(Endurance) }
		else if ms == .Wisdom    { train(Wisdom) }
		else if ms == .Intellect { train(Intellect) }
		else                     { train(Charisma) }
	}

	// Always be questing
	if !.QuestType {
		if Attempt_elixir_side_quest and !.Elixirs and .Act == 5 and .ActProgress == .ActDuration - 2 {
			// Attempt the side quest to find an elixir in Act V so as to
			// avoid the long search in Act VIII. TODO: Need to level up some
			// to be strong enough to complete that quest.
			var q = 80
			goto(Cholar)
			while .QuestLocation != Ritoli_Marsh {
				q += 1
				seekQuest(q)
			}
		} else {
			seekQuest(0)
		}
	}

	if .QuestLocation { goto(.QuestLocation) }

	if .QuestType == Cutscene {
		while .QuestProgress < .QuestQty {
			seek(QuestProgress)
		}
	} else if .QuestMob and .QuestObject {
		// Trophy hunting
		while .Health * 100 >= .MaxHealth * Safety_margin and
				.Health >= (4 <? .MaxHealth - 1)  and
				.Encumbrance < .Capacity and ..QuestObject < (.QuestQty - .QuestProgress) {
			seekVictim()
			use(Weapon)
			if !.MobHealth { loot(Trophies) }
		}
	} else if .QuestMob {
		// Killing
		while .Health * 100 >= .MaxHealth * Safety_margin and
				.QuestProgress < .QuestQty {
			seekVictim()
			use(Weapon)
			if !.MobHealth and .Encumbrance < .Capacity { loot(Trophies) }
		}
	} else if .QuestObject {
		// Foraging
		const qty = .QuestQty + (.QuestObject == Rations and 12 or 0)
		while .Encumbrance < .Capacity and ..QuestObject < (qty - .QuestProgress) {
			seek(.QuestObject)
			if .QuestObject <= Totem and ..QuestObject { break }  // can only carry 1
		}
	}

	if .QuestEnd { goto(.QuestEnd) }

	if .QuestObject { give(.QuestObject, ..QuestObject) }

	if .QuestType and .QuestProgress >= .QuestQty {
		completeQuest()
	}

	if .Location <= 36 { goto(Bompton) }
	else { goto(Sygnon_Tower) }

	if .Treasures {
		sell(Treasures, .Treasures)
	}

	if .Trophies {
		sell(Trophies, .Trophies)
	}

	if .Experience >= .ExperienceNeeded {
		levelUp()
	}

	while .Health < .MaxHealth { rest() }

	while upgradeEquipment() > 0 { }

	if .Encumbrance > .Capacity / 2 and .Location == Bompton { deposit(Gold, .Gold) }
}

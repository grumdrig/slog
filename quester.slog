/// Title: Let's Get Questing
/// Author: Grumdrig
/// Description: Complete quests until you can't no more.
target ChinbreakIsland_v1

macro goto(dest) {
	while dest != .Location and travel(dest) > 0 { }
}

macro minStat() .Strength <? .Agility <? .Endurance <? .Wisdom <? .Intellect <? .Charisma

func upgradeEquipment() {
	const least = (.Weapon / 5) <? .Armor <? .Shield <? .Headgear <? .Footwear <? .Mount
	const worst =
		(.Weapon / 5) == least and Weapon or
		.Armor == least and Armor or
		.Shield == least and Shield or
		.Headgear == least and Headgear or
		.Footwear == least and Footwear or
		Mount
	return worst == Weapon and buy(Weapon, .worst + 5) or buy(worst, .worst + 1)
}


while 1 {
	// Train
	while .TrainingPoints > 0 {
		const ms = minStat()
		if ms == .Strength       { train(Strength) }
		else if ms == .Agility   { train(Agility) }
		else if ms == .Endurance { train(Endurance) }
		else if ms == .Wisdom    { train(Wisdom) }
		else if ms == .Intellect { train(Intellect) }
		else                     { train(Charisma) }
	}

	// Always be questing
	if !.QuestType { seekQuest(0) }

	goto(.QuestLocation)

	if .QuestType == Cutscene {
		while .QuestProgress < .QuestQty {
			seek(QuestProgress)
		}
	} else if .QuestMob and .QuestObject {
		// Trophy hunting
		while .Health > (.MaxHealth / 2 >? (4 <? .MaxHealth - 1))  and
					.Encumbrance < .Capacity and ..QuestObject < (.QuestQty - .QuestProgress) {
			while .MobSpecies != .QuestMob or !.MobHealth or (.MobLevel > .Act and .Act < 9) {
				seek(MobSpecies)
			}
			use(Weapon)
			if !.MobHealth { loot(Trophies) }
		}
	} else if .QuestMob {
		// Killing
		while .Health > .MaxHealth / 2 and .QuestProgress < .QuestQty {
			while .MobSpecies != .QuestMob or !.MobHealth or (.MobLevel > .Act and .Act < 9) {
				seek(MobSpecies)
			}
			use(Weapon)
		}
	} else if .QuestObject {
		// Foraging
		while .Encumbrance < .Capacity and ..QuestObject < (.QuestQty - .QuestProgress) {
			seek(.QuestObject)
			if .QuestObject <= Totem and ..QuestObject { break }  // can only carry 1
		}
	}

	if .QuestEnd { goto(.QuestEnd) }

	if .QuestObject { give(.QuestObject, ..QuestObject) }

	if .QuestType and .QuestProgress >= .QuestQty {
		completequest()
	}

	if .Location <= 36 { goto(Bompton) }
	else { goto(Sygnon_Tower) }

	if .Treasures {
		sell(Treasures, .Treasures)
	}

	levelup()

	while .Health < .MaxHealth { rest() }

	while upgradeEquipment() > 0 { }

	if .Encumbrance > .Capacity / 2 { deposit(Gold, .Gold) }
}

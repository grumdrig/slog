/// Title: Let's Get Questing
/// Author: Grumdrig
/// Description: Complete quests until you can't no more.

startgame(1)

func initialize(stat, count) {
  while count {
    train(stat)
    count -= 1
  }
}


initialize(Strength, 4)
initialize(Agility, 1)
initialize(Endurance, 1)
initialize(Intellect, 1)
initialize(Wisdom, 1)
initialize(Charisma, 2)


macro goto(dest) {
  while travel(dest) > 0 { }
}

/* Cheat to the end
cheat(Act, 9)
cheat(ActProgress, 2)
seekquest()
goto(.QuestLocation)
while !seek(Totem) {}
goto (.QuestEnd)
give(Totem, 1)
completequest()
seekquest()
while .QuestMob != .MobSpecies { seek(MobSpecies) }
*/


drop(Food, .Food)

while 1 {
  if !.QuestEnd { seekquest() }
  goto(.QuestLocation)
  if .QuestMob and .QuestObject {
    // Trophy hunting
    while .Health > .MaxHealth / 2 and
          .Encumbrance < .Capacity and ..QuestObject < (.QuestQty - .QuestProgress) {
      while .MobSpecies != .QuestMob or !.MobHealth {
        seek(MobSpecies)
      }
      use(Weapon)
      if !.MobHealth { loot() }
    }
  } else if .QuestMob {
    // Killing
    while .Health > .MaxHealth / 2 and .QuestProgress < .QuestQty {
      while .MobSpecies != .QuestMob or !.MobHealth {
        seek(MobSpecies)
      }
      use(Weapon)
    }
  } else if .QuestObject {
    // Foraging
    while .Encumbrance < .Capacity and ..QuestObject < (.QuestQty - .QuestProgress) {
      seek(.QuestObject)
    }
  }

  goto(Bompton)

  if .QuestObject { give(.QuestObject, ..QuestObject) }

  completequest()
  viewcinematic()
  sell(Treasures, .Treasures)
  levelup()
  while .Health < .MaxHealth { rest() }
}

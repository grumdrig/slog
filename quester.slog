/// Target: Bompton Island v1.1
/// Title: Let's Get Questing
/// Author: Grumdrig
/// Description: Complete quests until you can't no more.

startgame(1)

func initialize(stat, count) {
  while count {
    train(stat)
    count -= 1
  }
}


initialize(StatStrength, 4)
initialize(StatAgility, 1)
initialize(StatEndurance, 1)
initialize(StatIntellect, 1)
initialize(StatWisdom, 1)
initialize(StatCharisma, 2)


const BOMPTON = 18

macro goto(dest) {
  while travel(dest) > 0 { }
}

drop(InventoryFood, .InventoryFood)

while 1 {
  if !.QuestOrigin { seekquest() }
  goto(.QuestLocation)
  if .QuestMob and .QuestObject {
    // Trophy hunting
    while .Health > .MaxHealth / 2 and
          .Encumbrance < .Capacity and ..QuestObject < (.QuestQty - .QuestProgress) {
      while .MobSpecies != .QuestMob or !.MobHealth {
        hunt()
      }
      melee()
      if !.MobHealth { loot() }
    }
  } else if .QuestMob {
    // Killing
    while .Health > .MaxHealth / 2 and .QuestProgress < .QuestQty {
      while .MobSpecies != .QuestMob or !.MobHealth {
        hunt()
      }
      melee()
    }
  } else if .QuestObject {
    // Foraging
    while .Encumbrance < .Capacity and ..QuestObject < (.QuestQty - .QuestProgress) {
      forage(.QuestObject)
    }
  }

  goto(BOMPTON)

  if .QuestObject { give(.QuestObject, ..QuestObject) }

  completequest()
  viewcinematic()
  sell(InventoryTreasures, .InventoryTreasures)
  levelup()
  while .Health < .MaxHealth { rest() }
}

target ChinbreakIsland_v1

/// Title: Slayer Freebird
/// Author: Grumdrig
/// Description: Greedily slay beasts at all costs. Not a game-winning strategy (and needing some improvement).


macro initialize(stat, count) {
	var count = count
	while count and .TrainingPoints {
		train(stat)
		count -= 1
	}
}

seekQuest(-1)

initialize(Strength, 4)
initialize(Agility, 1)
initialize(Endurance, 1)
initialize(Intellect, 2)
initialize(Wisdom, 1)
initialize(Charisma, 1)


macro goto(dest) {
	while travel(dest) > 0 { }
}

func goodHunting(forLevel) {
	const numGrounds = 8
	var huntingGrounds[numGrounds] = [12, 12, 17, 10, 16, 15, 33, 6]
	if forLevel < numGrounds {
		return huntingGrounds[forLevel]
	} else {
		return huntingGrounds[numGrounds - 1]
	}
}

seek(Weapon)

export "range 0 to 100:Percentage of full health at which to continue battle"
var Safety_margin = 33

while .Level < 88 {
	goto(goodHunting(.Level))
	while .Health * 100 >= .MaxHealth * Safety_margin {
		while !.MobHealth or .MobLevel > .Level {
			seek(MobSpecies)
		}
		while .MobHealth and .Health * 100 >= .MaxHealth * Safety_margin {
			use(Weapon)
		}
		if .Encumbrance < .Capacity and !.MobHealth { loot(Trophies) }
		if .MobHealth { seek(0) }  // run away
		// while .Health < .MaxHealth and .Reagents > 0 {
		//   if use(Spellbook1) <= 0 { break }
		// }
	}
	goto(18)
	if .Experience >= .ExperienceNeeded { levelUp() }
	while .Health < .MaxHealth {
		rest()
	}
	sell(Trophies, .Trophies)
	while .Encumbrance < .Capacity - 1 and buy(Reagents, 1) > 0 { }
}
